let
    Source = Csv.Document(File.Contents("D:\Supply_Chain Projects\Project_1\Final\Supply_chain_dataset - Raw.csv"),[Delimiter=",", Columns=22, Encoding=1252, QuoteStyle=QuoteStyle.None]),
    #"Promoted Headers" = Table.PromoteHeaders(Source, [PromoteAllScalars=true]),
    #"Transform Columnn Types" = Table.TransformColumnTypes(#"Promoted Headers",{{"price_per_unit", type number}, {"quality_score", type number}, {"delivery_time_days", Int64.Type}, {"on_time_delivery_rate", type number}, {"defect_rate", type number}, {"return_rate", type number}, {"delivery_mode", type text}, {"lead_time_variance", type number}, {"forecast_accuracy", type number}, {"seasonality_index", type number}, {"demand_volatility_index", type number}, {"order_frequency_monthly", Int64.Type}, {"avg_order_volume", type number}, {"payment_term_days", Int64.Type}, {"offer_validity_days", Int64.Type}, {"procurement_action_code", Int64.Type}, {"delivery_term_code", Int64.Type}, {"items_requested", Int64.Type}, {"items_offered", Int64.Type}, {"temporal_month", Int64.Type}, {"supplier_reliability_score", type number}, {"selected_supplier_flag", Int64.Type}}),
    
    #"AddSelectionStatus" = Table.AddColumn(#"Transform Columnn Types", "selection_status",
    each if [selected_supplier_flag] = 1 then "Selected"
    else "Rejected"
),
    #"Add Delivery Term Col" = Table.AddColumn(#"AddSelectionStatus", "delivery_term",
    each if [delivery_term_code] = 0 then "EXW"
         else if [delivery_term_code] = 1 then "FOB"
         else if [delivery_term_code] = 2 then "CIF"
         else if [delivery_term_code] = 3 then "DDP"
         else "Unknown",
    type text
),
    #"Add Procurement Action Col" = Table.AddColumn(#"Add Delivery Term Col", "procurement_action",
    each if [procurement_action_code] = 0 then "New Procurement"
         else if [procurement_action_code] = 1 then "Renewal/Reorder"
         else if [procurement_action_code] = 2 then "Amendment"
         else if [procurement_action_code] = 3 then "Extension"
         else "Unkown",
    type text
),
    #"Add Month Name Col" = Table.AddColumn(#"Add Procurement Action Col", "month_name",
    each Date.MonthName(#date(2020, [temporal_month], 1)),
    type text
),
    #"Add Delivery Performance Score Col" = Table.AddColumn(#"Add Month Name Col", "delivery_performance_score",
    each [on_time_delivery_rate] * (1-[defect_rate]),
    type number
),
    #"Add Order Accuracy Col" = Table.AddColumn(#"Add Delivery Performance Score Col", "order_accuracy",
    each (1 - [defect_rate]) * (1 - [return_rate]),
    type number
),
    #"Add On Time In Full Col" = Table.AddColumn(#"Add Order Accuracy Col", "on_time_in_full",
    each (1 - [on_time_delivery_rate]) * (1 - [defect_rate]) * (1 - [return_rate]),
    type number
),
    #"Add Risk Score Col" = Table.AddColumn(#"Add On Time In Full Col", "risk_score",
    each [defect_rate] + [return_rate] + [demand_volatility_index],
    type number
),
    #"Add Risk Category Col" = Table.AddColumn(#"Add Risk Score Col", "risk_category",
    each if [risk_score] <= 0.15 then "Low Risk"
         else if [risk_score] <= 0.3 then "Medium Risk"
         else if [risk_score] <= 0.5 then "High Risk"
         else "Extreme Risk",
    type text
),
    #"Add Value Score Col" = Table.AddColumn(#"Add Risk Category Col", "value_score",
    each [quality_score] / [price_per_unit],
    type number
),
   #"Add Value Category Col" = Table.AddColumn(#"Add Value Score Col", "value_category",
   each if [value_score] >= 0.10 then "Excellent"
        else if [value_score] >= 0.05 then "Good"
        else if [value_score] >= 0.02 then "Average"
        else "Poor",
    type text
),
   #"Add Price Category Col" = Table.AddColumn(#"Add Value Category Col", "price_category",
   each if [price_per_unit] <= 150 then "Low"
        else if [price_per_unit] <= 300 then "Medium"
        else "High",
    type text
),
   #"Add Lead_Time_Variance Col" = Table.AddColumn(#"Add Price Category Col", "lead_time_category",
   each if [lead_time_variance] <= 1 then "Very Consistent"
        else if [lead_time_variance] <= 2 then "Moderately Consistent"
        else if [lead_time_variance] <= 3 then "Inconsistent"
        else "Highly Inconsistent",
    type text
),
   #"Add Fulfillment Ratio Col" = Table.AddColumn(#"Add Lead_Time_Variance Col", "fulfillment_ratio percentage",
   each ([items_offered] / [items_requested]) * 100,
   type number 
),
  #"Add Fulfillment Category Col" = Table.AddColumn(
    #"Add Fulfillment Ratio Col",
    "fulfillment_category",
    each if [fulfillment_ratio percentage] > 100 then "Over-Delivery (>100%)"
        else if [fulfillment_ratio percentage] >= 97 then "World-Class (97%–100%)"
        else if [fulfillment_ratio percentage] >= 95 then "High (95–96.99%)"
        else if [fulfillment_ratio percentage] >= 85 then "Average (85–94.99%)"
        else if [fulfillment_ratio percentage] >= 80 then "Needs Improvement (80–84.99%)"
        else if [fulfillment_ratio percentage] < 80 and [fulfillment_ratio percentage] >= 0 then "Critical (<80%)"
        else null,
    type text
),
  #"Add Shipping Cost Estimated" = Table.AddColumn(
      #"Add Fulfillment Category Col",
      "shipping_cost_estimate",
      each  if [delivery_mode] = "Air" then [avg_order_volume] * 0.05
          else if [delivery_mode] = "Road" then [avg_order_volume] * 0.02
          else [avg_order_volume] * 0.01,
      type number
),
  
  #"Add Total Cost Impact" = Table.AddColumn(
      #"Add Shipping Cost Estimated",
      "total_cost_impact",
      each
          [shipping_cost_estimate]

           //Product Cost
          +([price_per_unit] * [avg_order_volume])

          //Defect Cost
          + ([price_per_unit] * [avg_order_volume] * [defect_rate])

          //Return Cost
          + ([price_per_unit] * [avg_order_volume] * [return_rate])

          //Lead Time Risk Cost: 0.02 is a risk scaling factor, not a real cost.
          + ([lead_time_variance] * [price_per_unit] * 0.02),
        type number
),    
  #"Add Customer Satisfaction" = Table.AddColumn(
      #"Add Total Cost Impact" ,
      "customer_satisfaction_estimate",
      each
            100 *
            (
                0.4 *
                    (if [delivery_mode] = "Air" then 1
                     else if [delivery_mode] = "Road" then 0.7
                     else if [delivery_mode] = "Sea" then 0.4
                     else 0.5)   ///Note

              + 0.3 * (1 - [defect_rate])
              + 0.2 * (1 - [return_rate])
              + 0.1 * (1 - [lead_time_variance] / 10)
            ),
        type number
)
in
    #"Add Customer Satisfaction"